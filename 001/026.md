Generous Azure Swallow

high

# Incorrect calculation of the BunniTokenPrice.

## Summary
When calculating the `BunniTokenPrice`, the uncollected fee is not considered.

## Vulnerability Detail
The `BunniPrice.sol#getBunniTokenPrice()` function determines the price of `bunniToken_` (representing a Uniswap V3 pool) in USD.
```solidity
    function getBunniTokenPrice(
        address bunniToken_,
        uint8 outputDecimals_,
        bytes calldata params_
    ) external view returns (uint256) {
        ...

        // Validate reserves
155     _validateReserves(
            _getBunniKey(token),
            lens,
            params.twapMaxDeviationsBps,
            params.twapObservationWindow
        );

        // Fetch the reserves
163     uint256 totalValue = _getTotalValue(token, lens, outputDecimals_);

        return totalValue;
    }
```
The _getTotalValue() function called in `L163` is as follow.
```solidity
    function _getTotalValue(
        BunniToken token_,
        BunniLens lens_,
        uint8 outputDecimals_
    ) internal view returns (uint256) {
220     (address token0, uint256 reserve0, address token1, uint256 reserve1) = _getBunniReserves(
            token_,
            lens_,
            outputDecimals_
        );
        uint256 outputScale = 10 ** outputDecimals_;

        // Determine the value of each reserve token in USD
        uint256 totalValue;
229     totalValue += _PRICE().getPrice(token0).mulDiv(reserve0, outputScale);
230     totalValue += _PRICE().getPrice(token1).mulDiv(reserve1, outputScale);

        return totalValue;
    }
```
The `_getTotalValue()` function determines the total value of the `Uniswap V3 Position` indicated by `token_` in USD.

The `_getBunnyReserves()` function called in `L220` is as follow.
```solidity
    function _getBunniReserves(
        BunniToken token_,
        BunniLens lens_,
        uint8 outputDecimals_
    ) internal view returns (address token0, uint256 reserve0, address token1, uint256 reserve1) {
        BunniKey memory key = _getBunniKey(token_);
198     (uint112 reserve0_, uint112 reserve1_) = lens_.getReserves(key);

        // Get the token addresses
        token0 = key.pool.token0();
        token1 = key.pool.token1();
        uint8 token0Decimals = ERC20(token0).decimals();
        uint8 token1Decimals = ERC20(token1).decimals();
        reserve0 = uint256(reserve0_).mulDiv(10 ** outputDecimals_, 10 ** token0Decimals);
        reserve1 = uint256(reserve1_).mulDiv(10 ** outputDecimals_, 10 ** token1Decimals);
    }
```
On `L198` the amount of reserved token0 and token1 are calculated, but uncolleted fee is not considered.

On the other hand, in the `BunniHelper.sol#getReservesRatio()` function, the reserve ratio is calculated with considering the uncollected fee.
As compared with this, the `BunniTokenPrice` should be calculated with considering collected fee.
The `BunniHelper.sol#getReservesRatio()` function is as follow.
```solidity
    /// @notice         Returns the ratio of token1 to token0 based on the position reserves
    /// @dev            Includes uncollected fees
    function getReservesRatio(BunniKey memory key_, BunniLens lens_) public view returns (uint256) {
        IUniswapV3Pool pool = key_.pool;
        uint8 token0Decimals = ERC20(pool.token0()).decimals();

        (uint112 reserve0, uint112 reserve1) = lens_.getReserves(key_);
        (uint256 fee0, uint256 fee1) = lens_.getUncollectedFees(key_);

        return (reserve1 + fee1).mulDiv(10 ** token0Decimals, reserve0 + fee0);
    }
```

## Impact
Unconsidering Fee in calculation of `BunniTokenPrice` can cause incorrect Price.

## Code Snippet
https://github.com/sherlock-audit/2023-11-olympus-web3-master/blob/main/bophades/src/modules/PRICE/submodules/feeds/BunniPrice.sol#L205-L206

## Tool used

Manual Review

## Recommendation
The `BunniPrice.sol#_getBunniReserves()` function should be rewritten as follow.
```solidity
    function _getBunniReserves(
        BunniToken token_,
        BunniLens lens_,
        uint8 outputDecimals_
    ) internal view returns (address token0, uint256 reserve0, address token1, uint256 reserve1) {
        BunniKey memory key = _getBunniKey(token_);
        (uint112 reserve0_, uint112 reserve1_) = lens_.getReserves(key);
+       (uint256 fee0, uint256 fee1) = lens_.getUncollectedFees(key);

        // Get the token addresses
        token0 = key.pool.token0();
        token1 = key.pool.token1();
        uint8 token0Decimals = ERC20(token0).decimals();
        uint8 token1Decimals = ERC20(token1).decimals();
-       reserve0 = uint256(reserve0_).mulDiv(10 ** outputDecimals_, 10 ** token0Decimals);
-       reserve1 = uint256(reserve1_).mulDiv(10 ** outputDecimals_, 10 ** token1Decimals);
+       reserve0 = uint256(reserve0_ + fee0).mulDiv(10 ** outputDecimals_, 10 ** token0Decimals);
+       reserve1 = uint256(reserve1_ + fee1).mulDiv(10 ** outputDecimals_, 10 ** token1Decimals);
    }
```