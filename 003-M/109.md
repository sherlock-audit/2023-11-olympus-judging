Early Daisy Goblin

medium

# removeCategoryGroup function does not update variables correctly

## Summary
In the OlympusTreasury contract, the removeCategoryGroup function was not updated correctly. This may result in incorrect statistics of balances for a category. 

## Vulnerability Detail
The `categoryToGroup` and `groupToCategories` variables were not updated correctly in the removeCategoryGroup function.
[OlympusTreasury.sol#L567-L583](https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/OlympusTreasury.sol#L567-L583)
```solidity
    function removeCategoryGroup(CategoryGroup group_) external override permissioned {
        // Check if the category group exists
        if (!_categoryGroupExists(group_)) revert TRSRY_CategoryGroupDoesNotExist(group_);

        // Remove category group
        uint256 len = categoryGroups.length;
        for (uint256 i; i < len; ) {
            if (fromCategoryGroup(categoryGroups[i]) == fromCategoryGroup(group_)) {
                categoryGroups[i] = categoryGroups[len - 1];
                categoryGroups.pop();
                break;
            }
            unchecked {
                ++i;
            }
        }
    }
```

The `categoryToGroup` variable in the contract represents the group to which a certain asset class belongs. For example, categoryA==>GroupB, if you remove GroupB, categoryA still belongs to GroupB. At this time, categoryA cannot be removed or added to another group.
[OlympusTreasury.sol#L591-L632](https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/OlympusTreasury.sol#L591-L632)
```solidity
    function addCategory(Category category_, CategoryGroup group_) external override permissioned {
        // Check if the category group exists
        if (!_categoryGroupExists(group_)) revert TRSRY_CategoryGroupDoesNotExist(group_);

        // Check if the category is valid
        if (fromCategory(category_) == bytes32(0))
            revert TRSRY_InvalidParams(0, abi.encode(category_));

        // Check if the category exists by seeing if it has a non-zero category group
        if (fromCategoryGroup(categoryToGroup[category_]) != bytes32(0))
            revert TRSRY_CategoryExists(category_);

        // Store category data
        categoryToGroup[category_] = group_;
        groupToCategories[group_].push(category_);
    }

    /// @inheritdoc TRSRYv1_1
    /// @dev        This function reverts if:
    /// @dev        - The caller is not permissioned
    /// @dev        - `category_` does not exist
    function removeCategory(Category category_) external override permissioned {
        // Check if the category exists by seeing if it has a non-zero category group
        CategoryGroup group = categoryToGroup[category_];
        if (fromCategoryGroup(group) == bytes32(0)) revert TRSRY_CategoryDoesNotExist(category_);

        // Remove category data
        categoryToGroup[category_] = toCategoryGroup(bytes32(0));

        // Remove category from group
        uint256 len = groupToCategories[group].length;
        for (uint256 i; i < len; ) {
            if (fromCategory(groupToCategories[group][i]) == fromCategory(category_)) {
                groupToCategories[group][i] = groupToCategories[group][len - 1];
                groupToCategories[group].pop();
                break;
            }
            unchecked {
                ++i;
            }
        }
    }
```

In the contract, `groupToCategories` represents all the categories in this group. This variable is not deleted correctly. If the removed group is added again, the previous relationship will still exist, which will cause the balance of the category to be incorrectly counted.

## Impact
This will cause certain category statuses to get stuck, and also cause category balances to be incorrectly counted. It will ultimately affect the exchange price of OHM.

## Code Snippet
[OlympusTreasury.sol#L567-L583](https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/OlympusTreasury.sol#L567-L583)
```solidity
    function removeCategoryGroup(CategoryGroup group_) external override permissioned {
        // Check if the category group exists
        if (!_categoryGroupExists(group_)) revert TRSRY_CategoryGroupDoesNotExist(group_);

        // Remove category group
        uint256 len = categoryGroups.length;
        for (uint256 i; i < len; ) {
            if (fromCategoryGroup(categoryGroups[i]) == fromCategoryGroup(group_)) {
                categoryGroups[i] = categoryGroups[len - 1];
                categoryGroups.pop();
                break;
            }
            unchecked {
                ++i;
            }
        }
    }
```

## Tool used

Manual Review

## Recommendation
It is recommended to thoroughly clear the relevant data when removing a group.