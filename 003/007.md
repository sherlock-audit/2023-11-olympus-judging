Polite Scarlet Antelope

medium

# `OlympusTreasury` cannot `removeAsset` if there are two or more locations

## Summary
`OlympusTreasury.removeAsset()` incorrectly reverts when there are two or more locations in the asset being removed.


## Vulnerability Detail
`OlympusTreasury.removeAsset()` removes the asset's locations with the [code below](https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/OlympusTreasury.sol#L470-L477)
```solidity
        len = asset.locations.length;
        for (uint256 i; i < len; ) {
            asset.locations[i] = asset.locations[len - 1];
            asset.locations.pop();
            unchecked {
                ++i;
            }
        }
```
- Outside the loop: `len` is assigned as the length of the location array.
- Inside the loop: the location array is popped (reduced in size). 
- Each loop iteration the length of `asset.locations` reduces in size by one, but `len` remains unchanged.
```solidity
            asset.locations[i] = asset.locations[len - 1];
```
When there is more than one loop iteration the above assigment guarantees an `Index out of bouds` revert.
_(`asset.locations` is reducing in length, but `len` is unchanged, eventually `len - 1` exceeds the array length)_

### PoC
Test reverting when attempting to remove an asset with two locations.
_(Place in [TRSRYv1_1.t.sol](https://github.com/sherlock-audit/2023-11-olympus/blob/main/bophades/src/test/modules/TRSRY.v1_1.t.sol) and run with `forge test --match-test test_remove_two_assets` and the test passes)_
```solidity
    function test_remove_two_assets() public {
        address[] memory locations = new address[](2);
        locations[0] = address(1);
        locations[1] = address(2);

        // Add the asset (that has two locations)
        vm.prank(godmode);
        TRSRY.addAsset(address(reserve), locations);

        // @audit removeAsset() suffers evm revert of "Index out of bounds"
        vm.expectRevert();
        vm.prank(godmode);
        TRSRY.removeAsset(address(reserve));
    }
```


## Impact
A valid path through a core feature under audit is unexpectedly reverting; requirement is not being satisfied.

Until an asset is removed, it is included in the Treasury accounting. 
Upstream policies using the Treasury balance in their decision-making could negatively be affected, and if the asset (collateral) became considered as malicious, `removeAsset` would be unable to remove it from the `OlympusTreasury`.

The issue can be worked around using `removeAssetLocation()` to reduce `asset.locations` to zero or one, then the asset could be successfully removed using `removeAsset`.

### Requirement
The expectation is from the previous [Supply and Treasury audit](https://github.com/sherlock-audit/2023-11-olympus/tree/main/bophades/audit/2023-11_spply_trsry#trsry-v11-module).
```text
TRSRY v1.1 (Module)

Features:
    Add/remove assets to be managed and tracked
```

Also in the NatSpec of [TRSRYv1_1.removeAsset](https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/TRSRY.v1.sol#L232-L235)
```solidity
    /// @notice             Removes an asset from tracking by the treasury
    /// @dev                Asset must be approved
    /// @param  asset_      Address of the asset to remove
    function removeAsset(address asset_) external virtual;
```

Then expanded upon in [OlympusTreasury](https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/OlympusTreasury.sol#L446-L450)
```solidity
    /// @inheritdoc TRSRYv1_1
    /// @dev        This function reverts if:
    /// @dev        - The caller is not permissioned
    /// @dev        - `asset_` is not approved
    function removeAsset(address asset_) external override permissioned {
```
`removeAsset` will:
- Remove the asset from the Treasury tracking
- Revert when call is not permissioned
- Revert when asset is not approved

There is no reason to expect `removeAsset` to revert when `asset_` has two or more locations, rather it is reasonable to expect the asset to be successfully removed.



## Code Snippet
https://github.com/sherlock-audit/2023-11-olympus/blob/9c8df76dc9820b4c6605d2e1e6d87dcfa9e50070/bophades/src/modules/TRSRY/OlympusTreasury.sol#L470-L477


## Tool used
Manual Review, Forge test


## Recommendation
The assigment moving the `location` to another array position is unnecessary (as all entries are to be removed anyway) and can be removed.

```diff
/bophades/src/modules/TRSRY/OlympusTreasury.sol#L472

-   asset.locations[i] = asset.locations[len - 1];
```
