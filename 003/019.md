Generous Azure Swallow

high

# OlympusTreasury.sol#removeAsset will be reverted with panic of out-of-bound error.

## Summary
When number of locations of asset is greater than 2, `OlympusTreasury.sol#removeAsset` function will be reverted with panic of out-of-bound error.

## Vulnerability Detail
`OlympusTreasury.sol#removeAsset` is the following.
```solidity
    function removeAsset(address asset_) external override permissioned {
        Asset storage asset = assetData[asset_];

        // Check that asset is approved
        if (!asset.approved) revert TRSRY_AssetNotApproved(asset_);

        // Remove asset
        uint256 len = assets.length;
        for (uint256 i; i < len; ) {
            if (assets[i] == asset_) {
                assets[i] = assets[len - 1];
                assets.pop();
                break;
            }
            unchecked {
                ++i;
            }
        }

        // Remove locations
        len = asset.locations.length;
        for (uint256 i; i < len; ) {
472:        asset.locations[i] = asset.locations[len - 1];
473:        asset.locations.pop();
            unchecked {
                ++i;
            }
        }

        // Remove categorization
        len = categoryGroups.length;
        for (uint256 i; i < len; ) {
            categorization[asset_][categoryGroups[i]] = toCategory(bytes32(0));
            unchecked {
                ++i;
            }
        }

        // Remove asset data
        delete assetData[asset_];
    }
```
As you can see above, in line 472, `asset.locations[i]` and `asset.locations[len-1]` variables are referenced.

Suppose that `len = asset.locations.length = 2`.
When `i = 0`, by line 473, `asset.locations.length` will be decreased to 1 and `asset.locations[1]` does not exist any more.
Therefore, for the next iteration with `i = 1 < len`, in line 472, the reference to `asset.locations[i]` and `asset.locations[len - 1]` will be reverted with panic of out-of-bound error.

## Impact
When the number of locations of asset is greater than 2, the asset can not be removed.
This means the Denial-of-Service.

## Code Snippet
https://github.com/sherlock-audit/2023-11-olympus-web3-master/blob/main/bophades/src/modules/TRSRY/OlympusTreasury.sol#L472

## Tool used
Manual Review

## Recommendation
`OlympusTreasury.sol#removeAssetL472` has to be removed as follows.
```solidity
    function removeAsset(address asset_) external override permissioned {
        Asset storage asset = assetData[asset_];

        // Check that asset is approved
        if (!asset.approved) revert TRSRY_AssetNotApproved(asset_);

        // Remove asset
        uint256 len = assets.length;
        for (uint256 i; i < len; ) {
            if (assets[i] == asset_) {
                assets[i] = assets[len - 1];
                assets.pop();
                break;
            }
            unchecked {
                ++i;
            }
        }

        // Remove locations
        len = asset.locations.length;
        for (uint256 i; i < len; ) {
 -          asset.locations[i] = asset.locations[len - 1];
            asset.locations.pop();
            unchecked {
                ++i;
            }
        }

        // Remove categorization
        len = categoryGroups.length;
        for (uint256 i; i < len; ) {
            categorization[asset_][categoryGroups[i]] = toCategory(bytes32(0));
            unchecked {
                ++i;
            }
        }

        // Remove asset data
        delete assetData[asset_];
    }
```