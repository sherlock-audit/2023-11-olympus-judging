Generous Azure Swallow

high

# OlympusTreasury.removeCategoryGroup function does not remove the group cleanly.

## Summary
The `OlympusTreasury.removeCategoryGroup` function removes `group_` from `categoryGroups` state variables but does not remove it from `categorization` state variables.
This may cause some serious problems.

For instance, after removing `group_`, when admin calls `removeAsset` function to remove a certain `asset_`, if the `asset_` was connected to some `categroy_` via the deleted `group_`, it does not remove the corresponding link.
Later, if admin calls `addAsset` function and adds again the `asset_` that was already removed, then `asset_` turns out to be already linked to `category_`, which is not intended by the administrator.

## Vulnerability Detail
For some `group_`, `asset_` and `category_`, let us suppose that `categorization[asset_][group_] = category_` holds.

Now, administrator calls `removeCategoryGroup` function and removes `group_`.
`OlympusTreasury.removeCategoryGroup` function is following.
```solidity
File: OlympusTreasury.sol
567:     function removeCategoryGroup(CategoryGroup group_) external override permissioned {
568:         // Check if the category group exists
569:         if (!_categoryGroupExists(group_)) revert TRSRY_CategoryGroupDoesNotExist(group_);
570: 
571:         // Remove category group
572:         uint256 len = categoryGroups.length;
573:         for (uint256 i; i < len; ) {
574:             if (fromCategoryGroup(categoryGroups[i]) == fromCategoryGroup(group_)) {
575:                 categoryGroups[i] = categoryGroups[len - 1];
576:                 categoryGroups.pop();
577:                 break;
578:             }
579:             unchecked {
580:                 ++i;
581:             }
582:         }
583:     }
```
As you can see from above, the function removes `group_` only from the state variable `categoryGroups`, and not from the state variable `categorization`.

Now let us suppose that administrator calls `removeAsset` function and remove `asset_`.
```solidity
    function removeAsset(address asset_) external override permissioned {
        ...
479:    // Remove categorization
        len = categoryGroups.length;
        for (uint256 i; i < len; ) {
            categorization[asset_][categoryGroups[i]] = toCategory(bytes32(0));
            unchecked {
                ++i;
            }
485:    }
        ...
    }
```
The intention of code `L479-L485` is to initialize the links from `asset_` to all categories.
But, since the `group_` is already removed from `categoryGroups`, the `categorization[asset_][group_]` value will not be initialized and `categorization[asset_][group_] = category_` still holds true.

If administrator calls `addAsset` function and adds again the already removed `asset_`, then `categorization[asset_][group_] = category_` forces that `asset_` is connected with `category_` automatically.
This means that `getCategoryBalance` function result will also contain the balance of `asset_`.

## Impact
Regardless of the manager's intention, some assets and categories can be linked to cause serious errors.

## Code Snippet
https://github.com/sherlock-audit/2023-11-olympus-web3-master/blob/main/bophades/src/modules/TRSRY/OlympusTreasury.sol#L567-L583

## Tool used
Manual Review

## Recommendation
Add codes to the `OlympusTreasury.removeCategoryGroup` function that initializes the value of the `categorization` state variables as follows.
```solidity
    function removeCategoryGroup(CategoryGroup group_) external override permissioned {
        // Check if the category group exists
        if (!_categoryGroupExists(group_)) revert TRSRY_CategoryGroupDoesNotExist(group_);

        // Remove category group
        uint256 len = categoryGroups.length;
        for (uint256 i; i < len; ) {
            if (fromCategoryGroup(categoryGroups[i]) == fromCategoryGroup(group_)) {
                categoryGroups[i] = categoryGroups[len - 1];
                categoryGroups.pop();
                break;
            }
            unchecked {
                ++i;
            }
        }
 +      len = assets.length;
 +      for (uint256 i; i < len; ) {
 +          categorization[assets[i]][group_] = toCategory(bytes32(0));
 +          unchecked {
 +              ++i;
 +          }
 +      }
    }
```