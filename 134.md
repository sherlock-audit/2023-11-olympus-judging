Expert Hemp Sidewinder

medium

# Rounding errors in `BalancerPoolTokenPrice#_getWeightedPoolRawValue()` can produce vastly incorrect LP token prices

## Summary

Via a bug in `BalancerPoolTokenPrice#_getWeightedPoolRawValue()`, `BalancerPoolTokenPrice#getWeightedPoolTokenPrice()` can produce wildly inaccurate prices in specific scenarios.

## Vulnerability Detail

In L330 of `BalancerPoolTokenPrice.sol`, the following block is used either to initialize `poolValue` or to multiply it into the existing product:

```solidity
if (poolValue == 0) {
    poolValue = currentValue;
} else {
    poolValue = poolValue.mulDiv(currentValue, 10 ** outputDecimals_);
}
```

It seems that the intention behind this block is to initialize `poolValue` on the first iteration of the loop, but that is not actually what the code does. It initializes `poolValue` whenever it is equal to zero; this might be on the first iteration, or it could be on any future iteration (provided that all prior iterations also had a `poolValue` of zero). In other words, for as many iterations N for which `currentValue` is zero, the `poolValue` is considered uninitialized.

For example, consider the following hypothetical iterations:
```markdown
i=0:   poolValue = 0, currentValue = 0 => poolValue = 0
...
i=N-1: poolValue = 0, currentValue = 0 => poolValue = 0
i=N:   poolValue = 0, currentValue = X => poolValue = X
...
```

On iteration N, `poolValue` is simply set to `currentValue` on the false assumption that this is an initialization. But the total `poolValue` should actually be zero because one or more of the product's components is zero. The correct version of those hypothetical iterations looks like this:
```markdown
i=0:   poolValue = 0, currentValue = 0 => poolValue = 0
...
i=N-1: poolValue = 0, currentValue = 0 => poolValue *= 0 => poolValue = 0
i=N:   poolValue = 0, currentValue = X => poolValue *= X => poolValue = 0
...
```

It is indeed possible for `currentValue` to be zero given that it does not come directly from the price feed. It is computed from `_getTokenValueInWeightedPool()` as follows:

```solidity
uint256 price; // Scale: `WEIGHTED_POOL_POW_DECIMALS`
{
    /**
     * PRICE will revert if there is an issue resolving the price, or if it is 0.
     *
     * As the value of the pool token is reliant on the price of every underlying token,
     * the revert from PRICE is not caught.
     */
    (uint256 price_, ) = _PRICE().getPrice(token_, PRICEv2.Variant.CURRENT); // Scale: `outputDecimals_`

    price = price_.mulDiv(10 ** WEIGHTED_POOL_POW_DECIMALS, 10 ** outputDecimals_);
}

// Scale: `WEIGHTED_POOL_POW_DECIMALS`
uint256 weight = weight_.mulDiv(10 ** WEIGHTED_POOL_POW_DECIMALS, 10 ** poolDecimals_);

// All inputs to pow need to be in the scale of WEIGHTED_POOL_POW_DECIMALS, so adjust for that
uint256 value = LogExpMath.pow(
    price.mulDiv(10 ** WEIGHTED_POOL_POW_DECIMALS, weight),
    weight
); // Scale: `WEIGHTED_POOL_POW_DECIMALS`

// @audit Even a nonzero `price` can produce a `value` that rounds down to zero here,
//        given sufficiently small price and requested precision (`outputDecimals_`).
// Adjust for `outputDecimals_`
return value.mulDiv(10 ** outputDecimals_, 10 ** WEIGHTED_POOL_POW_DECIMALS);
```

## Impact

`BalancerPoolTokenPrice#getWeightedPoolTokenPrice()` can produce wildly inaccurate prices if any one constituent price is close enough to zero and the requested precision is small. For example, consider a hypothetical pool of two constituent assets:

```markdown
asset0Price = $0.001
asset1Price = $1917.25

asset0Weight = 0.8
asset1Weight = 0.2

outputDecimals = 2

Current output:
BalancerPoolTokenPrice#_getWeightedPoolRawValue():
    iteration 0:
        price = 0.001
        weight = 0.8
        value = (0.001 * 0.8) ** 0.8 = 0.00333
        currentValue = value scaled to 2 decimals => 0.00
        poolValue = currentValue => 0
    iteration 1:
        price = 1917.25
        weight = 0.2
        value = (1917.25 * 0.2) ** 0.2 = 3.29
        currentValue = value scaled to 2 decimals => 3.29
        poolValue = currentValue => 3.29 (bug here: should be 0, but the poolValue == 0 check prevents this)
    return poolValue = 3.29

Desired output:
BalancerPoolTokenPrice#_getWeightedPoolRawValue():
    iteration 0:
        price = 0.001
        weight = 0.8
        value = (0.001 * 0.8) ** 0.8 = 0.00333
        currentValue = value scaled to 2 decimals => 0.00
        poolValue *= currentValue => 0
    iteration 1:
        price = 1917.25
        weight = 0.2
        value = (1917.25 * 0.2) ** 0.2 = 3.29
        currentValue = value scaled to 2 decimals => 3.29
        poolValue *= currentValue => 0
    return poolValue = 0

Even given hypothetically infinite precision, the correct output is 0.00333 * 3.29 = ~0.011, rather than 3.29.
```

Here, the price returned by `_getWeightedPoolRawValue()` was 3.29, whereas it should have been zero (or close to zero, given infinite precision). This propagates up to `getWeightedPoolTokenPrice()` where a very incorrect price is returned.

Note that this particular example depends on a separate finding in which `_PRICE().getPrice()` uses a different precision than `outputDecimals_`, but there may be other edge cases with matching precision. Fuzz testing various price/weight combinations would uncover this, but current fuzz testing focuses only on decimal precision.

## Code Snippet

https://github.com/sherlock-audit/2023-11-olympus/blob/main/bophades/src/modules/PRICE/submodules/feeds/BalancerPoolTokenPrice.sol#L253-L342

## Tool used

Manual Review

## Recommendation

The check is unnecessary. It is in place simply because of the incorrect initialization of `poolValue` to zero. It should instead be initialized to one, as follows:

```solidity
function _getWeightedPoolRawValue(
    address[] memory tokens_,
    uint256[] memory weights_,
    uint8 poolDecimals_,
    uint8 outputDecimals_,
    bytes32 poolId_
) internal view returns (uint256) {
    uint256 len = tokens_.length;

    // @audit Initialize to one (10 ** outputDecimals_) instead of zero.
    uint256 poolValue = 10 ** outputDecimals_; // Scale: `outputDecimals_`
    // @audit Original code block.
    // uint256 poolValue = 0; // Scale: `outputDecimals_`
    for (uint256 i; i < len; ) {
        uint256 currentValue = _getTokenValueInWeightedPool(
            tokens_[i],
            weights_[i],
            poolDecimals_,
            outputDecimals_,
            poolId_,
            i
        );

        // @audit Initializing to one, above, allows for removing the check here.
        poolValue = poolValue.mulDiv(currentValue, 10 ** outputDecimals_);
        // @audit Original code block.
        // if (poolValue == 0) {
        //     poolValue = currentValue;
        // } else {
        //     poolValue = poolValue.mulDiv(currentValue, 10 ** outputDecimals_);
        // }

        unchecked {
            ++i;
        }
    }

    return poolValue;
}
```