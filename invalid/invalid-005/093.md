Daring Malachite Lizard

high

# UniswapV2 pool token price may not be correctly calculated due to incorrect calculation of pool supply

## Summary
When calculating UniswapV2 pool token price, if any of the underlying token decimals is different from `outputDecimals_`, incorrect price will be returned due to `poolSupply_` is wrongly calculated.

## Vulnerability Detail
In [getPoolTokenPrice()](https://github.com/sherlock-audit/2023-11-olympus/blob/main/bophades/src/modules/PRICE/submodules/feeds/UniswapV2PoolTokenPrice.sol#L192-L196) function, `outputDecimals_` indicates the number of decimals to return the price in.

Token balances are scaled by `outputDecimals_`:
```solidity
balance0 = uint256(balances[0]).mulDiv(10 ** outputDecimals_, 10 ** token0Decimals);
balance1 = uint256(balances[1]).mulDiv(10 ** outputDecimals_, 10 ** token1Decimals);
```
`k` is scaled by `outputDecimals_`:
```solidity
k = balance0.mulDiv(balance1, 10 ** outputDecimals_);
```
and `poolSupply` is scaled too:
```solidity
poolSupply = poolSupply_.mulDiv(10 ** outputDecimals_, 10 ** poolDecimals);
```
Token prices are retrieved from [_PRICE()](https://github.com/sherlock-audit/2023-11-olympus/blob/main/bophades/src/modules/PRICE/PRICE.v2.sol#L372) function and are in `outputDecimals_`:
```solidity
(uint256 price0_, ) = PRICE.getPrice(token0, PRICEv2.Variant.CURRENT);
(uint256 price1_, ) = PRICE.getPrice(token1, PRICEv2.Variant.CURRENT);

price0 = price0_;
price1 = price1_;
```
At last, pool token price will be calculated based on the [equation](https://cmichel.io/pricing-lp-tokens/):

<img width="452" alt="截屏2023-12-23 20 44 28" src="https://github.com/sherlock-audit/2023-11-olympus-Coinstein/assets/16879758/69d75559-c65e-41a2-ad29-8ebb12a496b3">

```solidity
uint256 poolValue; // outputDecimals_
{
        uint256 priceMultiple = FixedPointMathLib.sqrt(
                price0.mulDiv(price1, 10 ** outputDecimals_) * k
        ); // sqrt(price * price) = outputDecimals_

        uint256 two = 2 * 10 ** outputDecimals_;
        poolValue = two.mulDiv(priceMultiple, poolSupply);
}
```
However, the price will be wrong if any of the underlying token decimals is different from `outputDecimals_`.

Assume `outputDecimals_` is **18**, token0 decimals is **6** and balance is **1e6**, token1 decimals is **18** and balance is **1e18**, hence pool total supply is `Math.sqrt(1e6 * 1e18) = 1e12` based on [UniswapV2Pair](https://github.com/Uniswap/v2-core/blob/ee547b17853e71ed4e0101ccfd52e70d5acded58/contracts/UniswapV2Pair.sol#L119-L124).
When calculating pool token price, suppose both tokens' price are 1u, we can get:
> price0 = 1e18, price1 = 1e18
> balance0 = 1e18, balance1 = 1e18
> k = 1e18
> poolSupply = 1e12

And finally we got:
> poolValue = 2 * 1e18 * Math.sqrt(price0 * price1 / 1e18 * k) / poolSupply = 2000000e18

Pool token price is **2000000u** and this is obviously wrong (the correct price should be 2u).

The culprit is that `poolSupply` is not calculated correctly, originally in the pool, `poolSupply` is equal to `Math.sqrt(original balance0 * original balance1)`, because token balances are scaled to `outputDecimals_`, `poolSupply` is supposed to be scaled too.

Unfortunately, as `poolDecimals` is the same as `outputDecimals_`, so `poolSupply` is not changed, hence the price is wrongly calculated.

Please run the test to verify:
```solidity
contract AuditUniswapV2PoolTokenPriceTest is Test {
    uint256 mainnetFork;
    string MAINNET_RPC_URL = vm.envString("MAINNET_RPC_URL");

    address user = makeAddr("user");

    MockERC20 tokenA;
    MockERC20 tokenB;

    MockPrice internal mockPrice;
    IUniswapV2Pair pair;

    uint8 internal PRICE_DECIMALS = 18;
    UniswapV2PoolTokenPrice internal uniswapSubmodule;

    function setUp() public {
        mainnetFork = vm.createSelectFork(MAINNET_RPC_URL);

        uint8 decimalsA = 6;
        uint8 decimalsB = 18;

        tokenA = new MockERC20("Token A", "A", decimalsA);
        tokenB = new MockERC20("Token B", "B", decimalsB);

        deal(address(tokenA), user, 10 ** decimalsA);
        deal(address(tokenB), user, 10 ** decimalsB);

        // Create pair
        pair = IUniswapV2Pair(IUniswapV2Factory(0x5C69bEe701ef814a2B6a3EDD4B1652CB9cc5aA6f).createPair(address(tokenA), address(tokenB)));
        IUniswapV2Router02 router = IUniswapV2Router02(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);

        // Add liquidity
        vm.startPrank(user);
        tokenA.approve(address(router), type(uint256).max);
        tokenB.approve(address(router), type(uint256).max);
        router.addLiquidity(address(tokenA), address(tokenB), 10 ** decimalsA, 10 ** decimalsB, 0, 0, user, block.timestamp);
        vm.stopPrank();

        // pool total supply is Math.sqrt(reserveA * reserveB);
        assertEq(pair.totalSupply(), FixedPointMathLib.sqrt((10 ** decimalsA) * (10 ** decimalsB)));

        // Set up the UniswapV2 submodule
        {
            mockPrice = new MockPrice(new Kernel(), PRICE_DECIMALS, uint32(8 hours));
            mockPrice.setTimestamp(uint48(block.timestamp));

            uniswapSubmodule = new UniswapV2PoolTokenPrice(mockPrice);

            // tokenA price: 1u
            mockPrice.setPrice(address(tokenA), 10 ** PRICE_DECIMALS);
            // tokenB price: 1u
            mockPrice.setPrice(address(tokenB), 10 ** PRICE_DECIMALS);
        }
    }

    function test_audit_uniswapV2_getPoolTokenPrice() public {
        uint256 expectedPoolTokenPrice = pricingLPtokens(pair, mockPrice.getPrice(address(tokenA)), mockPrice.getPrice(address(tokenB)));
        uint256 actualPoolTokenPrice = uniswapSubmodule.getPoolTokenPrice(address(0), PRICE_DECIMALS, abi.encode(pair));

        // Expected pool token price: 2u
        assertEq(expectedPoolTokenPrice, 2e18);
        // Actual pool token price: 2000000u
        assertEq(actualPoolTokenPrice, 2e24);

        // Actual pool token price is 1000000 times larger than expected, which is obviously wrong
        assertEq(actualPoolTokenPrice, expectedPoolTokenPrice * 1000000);
    }

    // https://cmichel.io/pricing-lp-tokens/
    function pricingLPtokens(IUniswapV2Pair pair_, uint256 price0_, uint256 price1_) private view returns (uint256) {
        (uint256 reserve0, uint256 reserve1, ) = pair_.getReserves();
        uint256 k = reserve0 * reserve1;
        return 2 * FixedPointMathLib.sqrt(price0_ * price1_ * k) / pair_.totalSupply();
    }
}
```

## Impact
Incorrect UniswapV2 pool token price will be returned, this causes economical impact as Olympus could be exchanging OHM for treasury assets at a wrong price.

## Code Snippet
https://github.com/sherlock-audit/2023-11-olympus/blob/main/bophades/src/modules/PRICE/submodules/feeds/UniswapV2PoolTokenPrice.sol#L262

## Tool used
Manual Review

## Recommendation
`poolSupply` should be calculated based on `balance0` and `balance1`, instead of scaled by `outputDecimals_` directly:
```solidity
poolSupply = FixedPointMathLib.sqrt(balance0) * FixedPointMathLib.sqrt(balance1); // avoid overflow error
``` 